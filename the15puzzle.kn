{{{
	15パズル for Kuin 1.00
	Last Modified: 2013/10/27 13:47:05.

	作成:
		Tatt (@tatt61880)
		https://twitter.com/tatt61880
}}}

const PieceSize: int :: 60
const ShuffleNum: int :: 1000
const PixelPerFrame: int :: @PieceSize / 4
const FontSize: int :: (@PieceSize$float * 0.7)$ int
const FontName: []char :: "mplus-1m-regular.ttf"

var Font: Draw@CFont
enum Dir
	Stay
	Right :: 1
	Left :: 2
	Up :: 3
	Down :: 4
end enum

class Piece()
	-var id: int
	-var num: int
	-var tex: Draw@CTex
	var posId: int
	var pos: Pos
	var dir: @Dir

	func Init(id: int): @Piece
		do this.id :: id
		do this.num :: id + 1
		do this.tex :: @Font.MakeTex(@FontSize, this.num.ToStr(), 3)
		do this.posId :: id
		do this.pos :: (#Pos).Init(this.posId)
		do this.dir :: @Dir#Stay
		return this
	end func

	func GetId(): int
		return this.id
	end func

	func Move(pixelPerFrame: int)
		if(this.dir = @Dir#Right)
			do this.pos.x :+ pixelPerFrame
		elif(this.dir = @Dir#Left)
			do this.pos.x :- pixelPerFrame
		elif(this.dir = @Dir#Up)
			do this.pos.y :- pixelPerFrame
		elif(this.dir = @Dir#Down)
			do this.pos.y :+ pixelPerFrame
		end if
		if(this.pos.x % @PieceSize = 0 & this.pos.y % @PieceSize = 0)
			do this.dir :: @Dir#Stay
		end if
	end func

	func Draw()
		do Draw@Rect(this.pos.x$ float + 2.0, this.pos.y$ float + 2.0, @PieceSize$ float - 4.0, @PieceSize$ float - 4.0, 1.0, 1.0, 1.0, 1.0)
		do this.tex.Draw(0.0, 0.0, this.tex.Width(), this.tex.Height(), this.pos.x$ float + (@PieceSize$ float - this.tex.Width()) / 2.0, this.pos.y$ float, 0.0, 0.0, 1.0, 1.0)
	end func

	class Pos()
		var x: int
		var y: int
		func Init(posId: int): Pos
			do this.x :: @PieceSize * (posId % 4)
			do this.y :: @PieceSize * (posId / 4)
			return this
		end func
	end class
end class
var Pieces: []@Piece
var VoidPiece: @Piece

{駒の位置情報を元にその駒のidを返します}
func FindPieceID(posId: int): int
	foreach p(@Pieces)
		if(p.posId = posId)
			return p.GetId()
		end if
	end foreach
	do Dbg@Log("駒が見つかりません。これはおかしいです。")
	assert false
end func

func Init(cfg: Kuin@CCfg)
	do cfg.Title :: "THE 15 PUZZLE"
end func

func Main()
	assert @PieceSize % @PixelPerFrame = 0 {この条件が満たされている前提で駒の移動処理を行います}
	{フォントの初期化}
	do @Font :: Draw@LoadFont(@FontName)
	{駒の初期化}
	do @Pieces :: #[16]@Piece
	for i(0, 15)
		do @Pieces[i] :: (#@Piece).Init(i)
	end for
	do @VoidPiece :: @Pieces[15]
	do @ShufflePieces()

	while a()
		do Kuin@Act()
		do drawPieces()
		foreach p(@Pieces) {移動アニメーション用のループです}
			if(p.dir <> @Dir#Stay)
				do p.Move(@PixelPerFrame)
				continue a {移動中にマウス入力を受け付けないように、ここでwhileループの先頭に戻ります}
			end if
		end foreach

		do @SetDirection(false)
		if(Input@Pad(0, Input@EBtn#A) > 0)
			do @ShufflePieces()
		end if
	end while

	func drawPieces()
		foreach p(@Pieces)
			if(p <>& @VoidPiece)
				do p.Draw()
			end if
		end foreach
	end func
end func

func SetDirection(shuffleMode: bool)
	var id: int
	var dir: @Dir
	if(shuffleMode)
		do dir :: Rnd@Get(1, 4)$ @Dir
	else
		if(Input@Pad(0, Input@EBtn#Right) % 15 = 1)
			do dir :: @Dir#Right
		elif(Input@Pad(0, Input@EBtn#Left) % 15 = 1)
			do dir :: @Dir#Left
		elif(Input@Pad(0, Input@EBtn#Up) % 15 = 1)
			do dir :: @Dir#Up
		elif(Input@Pad(0, Input@EBtn#Down) % 15 = 1)
			do dir :: @Dir#Down
		end if
	end if

	switch d(dir)
	case(@Dir#Right)
		if(@VoidPiece.posId % 4 <> 0)
			do id :: @FindPieceID(@VoidPiece.posId - 1)
			do @Pieces[id].dir :: d
			do @VoidPiece.posId :$ @Pieces[id].posId
		end if
	case(@Dir#Left)
		if(@VoidPiece.posId % 4 <> 3)
			do id :: @FindPieceID(@VoidPiece.posId + 1)
			do @Pieces[id].dir :: d
			do @VoidPiece.posId :$ @Pieces[id].posId
		end if
	case(@Dir#Up)
		if(@VoidPiece.posId / 4 <> 3)
			do id :: @FindPieceID(@VoidPiece.posId + 4)
			do @Pieces[id].dir :: d
			do @VoidPiece.posId :$ @Pieces[id].posId
		end if
	case(@Dir#Down)
		if(@VoidPiece.posId / 4 <> 0)
			do id :: @FindPieceID(@VoidPiece.posId - 4)
			do @Pieces[id].dir :: d
			do @VoidPiece.posId :$ @Pieces[id].posId
		end if
	end switch

	if(shuffleMode)
		do @Pieces[id].Move(@PieceSize)
	end if
end func

func ShufflePieces()
	for i(1, @ShuffleNum)
		do @SetDirection(true)
	end for
end func

